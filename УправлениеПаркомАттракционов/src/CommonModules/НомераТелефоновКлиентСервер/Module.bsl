
#Область ПрограммныйИнтерфейс

// Возвращает нормализованный номер телефона.
//
// Параметры:
//   Телефон - Строка - Исходный номер телефона
//   ВызыватьИсключение - Булево - Флаг, указывающий, вызывать ли исключение при ошибке
//
// Возвращаемое значение:
//   Строка - Нормализованный номер телефона или пустая строка в случае ошибки
Функция НормализованныйНомерТелефона(Телефон, ВызыватьИсключение = Ложь) Экспорт
	
	Цифра = "0123456789";
	ЦифрыТелефона = "";
	
	Для Сч = 1 По СтрДлина(Телефон) Цикл
		Символ = Сред(Телефон, Сч, 1);
		
		Если СтрНайти(Цифра, Символ) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ЦифрыТелефона = ЦифрыТелефона + Символ;
	КонецЦикла;
	
	Если СтрНачинаетсяС(ЦифрыТелефона, "8") Тогда
		ЦифрыТелефона = "7" + Сред(ЦифрыТелефона, 2);
	КонецЕсли;
	
	Ошибки = Новый Массив;
	
	Если Не СтрНачинаетсяС(ЦифрыТелефона, "7") Тогда
		Ошибки.Добавить(НСтр("ru = 'Номер телефона должен начинаться с цифры 7 или 8.'"));
	КонецЕсли;
	
	Если Не СтрДлина(ЦифрыТелефона) = 11 Тогда
		Ошибки.Добавить(НСтр("ru = 'Номер телефона должен состоять из 11 цифр.'"));
	КонецЕсли;
	
	Если Ошибки.Количество() > 0 Тогда
		Если ВызыватьИсключение Тогда
			ВызватьИсключение СтрСоединить(Ошибки, Символы.ПС);
		Иначе
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЦифрыТелефона;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти
